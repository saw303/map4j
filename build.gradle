import java.time.LocalDate
import java.time.LocalDateTime

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:${artifactoryPluginVersion}"
        classpath "org.codehaus.groovy:groovy:${groovyVersion}"
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    version = '0.0.6'

    dependencies {
        // The production code uses the SLF4J logging API at compile time
        compile "org.slf4j:slf4j-api:${slf4jVersion}"

        if (project.name != 'map4j-api') {
            compile project(':map4j-api')

            testCompile project(':map4j-testutil')
        }

        testCompile "org.spockframework:spock-core:${spockVersion}"
        testCompile "org.codehaus.groovy:groovy:${groovyVersion}"
    }

    jar {
        manifest {
            attributes 'Implementation-Title': project.name,
                    'Implementation-Version': version
        }
    }

    publishing {
        publications {
            JavaPublication(MavenPublication) {
                from components.java

                artifact sourcesJar {
                    classifier "sources"
                }

                groupId 'ch.stefanloosli.map4j'
                artifactId project.name
                version project.version
            }
        }
    }

    bintray {
        user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
        key = project.hasProperty('bintrayKey') ? project.property('bintrayKey') : System.getenv('BINTRAY_API_KEY')
        publications = ['JavaPublication']
        pkg {
            repo = 'releases'
            name = 'map4j'
            licenses = ['Apache-2.0']
            vcsUrl = 'https://github.com/loosli/map4j.git'
            labels = ['orika', 'mapper', 'dozer']
            publicDownloadNumbers = true
            version {
                name = project.version
                desc = 'map4j'
                vcsTag = project.version
                //released = LocalDateTime.now()
            }
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allJava

        from("${rootProject.projectDir}") {
            include "LICENSE.txt"
            into "META-INF"
            expand(dateOfYear: LocalDate.now().getYear())
        }
    }

    artifacts {
        archives sourcesJar
    }

}

project('map4j-dozer') {

    dependencies {

        compile "net.sf.dozer:dozer:${dozerVersion}"
    }
}

project('map4j-orika') {

    dependencies {

        compile("ma.glasnost.orika:orika-core:${orikaVersion}")
    }
}

project('map4j-testutil') {

}
